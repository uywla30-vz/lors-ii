incorporate "ss_tokens.inc"
incorporate "ss_ast.inc"
incorporate "ss_utils.inc"
incorporate "ss_lexer.inc"
incorporate "ss_parser.inc"
incorporate "memoria.inc"
incorporate "llvm_algebra.inc"
incorporate "traductor.inc"

algorithm genesis() -> whole
begin
    verify (arg_count() < 2) then
        reveal("Usage: lors_lw <script>.lr");
        exit_program(1);
    conclude

    datum input_file : series = arg_value(1);

    // Check extension
    datum len : whole = length(input_file);
    verify (len < 3 or not (substring(input_file, len - 3, 3) == ".lr")) then
        reveal("Error: Input file must have .lr extension");
        exit_program(1);
    conclude

    verify (file_exists(input_file) == false) then
        reveal("Error: File '" + input_file + "' not found");
        exit_program(1);
    conclude

    // 1. Read Source & Preprocess
    datum raw_code : series = file_read(input_file);
    datum input_dir : series = get_dirname(input_file);

    datum processed_code : series = preprocess_includes(raw_code, input_dir);

    // 2. Lex
    lexer_init(processed_code);
    tokenize();

    // 3. Parse
    parser_init();
    datum prog_node : whole = parse_program();

    // 4. CodeGen (LLVM IR)
    translate_program(prog_node);

    // 5. Write Output
    datum base_name : series = substring(input_file, 0, len - 3);
    datum asm_file : series = base_name + ".ll";

    file_write(asm_file, codegen_output);

    result 0;
end
